allprojects {
    group 'org.jetbrains.dokka'
    version dokka_version

    buildscript {
        repositories {
            mavenCentral()
            jcenter()
            maven {
                url  "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
            }
            maven {
                url "https://dl.bintray.com/kotlin/kotlin-dev"
            }
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
            classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'

            classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url 'https://jitpack.io' }
    }
}


def bintrayPublication(project, List<String> _publications) {
    configure(project, {
        apply plugin: 'com.jfrog.bintray'

        bintray {
            user = System.getenv('BINTRAY_USER')
            key = System.getenv('BINTRAY_KEY')

            pkg {
                repo = dokka_publication_channel
                name = 'dokka'
                userOrg = 'kotlin'
                desc = 'Dokka, the Kotlin documentation tool'
                vcsUrl = 'https://github.com/kotlin/dokka.git'
                licenses = ['Apache-2.0']
                version {
                    name = dokka_version
                }
            }

            publications = _publications
        }
    })
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}