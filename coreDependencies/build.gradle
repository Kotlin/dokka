def intellijCoreAnalysis() {
    return zipTree(project(":").configurations.intellijCore.singleFile).matching ({
        include("intellij-core-analysis.jar")
    })
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile intellijCoreAnalysis()
    compile "org.jetbrains.kotlin:kotlin-plugin-ij193:$kotlin_plugin_version" //TODO: parametrize ij version after 1.3.70
}

shadowJar {
    baseName = 'dokka-dependencies'
    classifier = ''

    configurations {
        exclude compileOnly
    }


    exclude 'colorScheme/**'
    exclude 'fileTemplates/**'
    exclude 'inspectionDescriptions/**'
    exclude 'intentionDescriptions/**'
    exclude 'tips/**'
    exclude 'messages/**'

    exclude 'src/**'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        dokkaDependencies(MavenPublication) { publication ->
            artifactId = 'dokka-dependencies'
            project.shadow.component(publication)
        }
    }
}

bintrayPublication(project, ["dokkaDependencies"])